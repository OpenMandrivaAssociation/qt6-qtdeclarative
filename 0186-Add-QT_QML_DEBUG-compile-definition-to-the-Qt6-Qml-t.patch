From 9917e12657330614b07f1cd9002bd473ff1a7277 Mon Sep 17 00:00:00 2001
From: Alexey Edelev <alexey.edelev@qt.io>
Date: Wed, 27 Aug 2025 12:54:27 +0200
Subject: [PATCH 186/239] Add QT_QML_DEBUG compile definition to the Qt6::Qml
 target

Consider the QT_ENABLE_QML_DEBUG target property when building
QML executables and add the QT_QML_DEBUG compiler definition to enable
QML debugging. The property is defined and intialized with the
QT_ENABLE_QML_DEBUG variable value on target creation. But can be set
manually.

TODO: Leave the flag undocumented in this commit. Since it's the public
API change, we only document it in further releases, but QtC may use it
with all supported Qt versions the undocumented "private" way.

Pick-to: 6.9 6.8
Task-number: QTBUG-139551
Change-Id: I94d4269981f87d6c226d7565257786658d68f992
Reviewed-by: Alexandru Croitor <alexandru.croitor@qt.io>
Reviewed-by: Ulf Hermann <ulf.hermann@qt.io>
Reviewed-by: Joerg Bornemann <joerg.bornemann@qt.io>
(cherry picked from commit 12af84f81b7597d1e3458341669101972750fd1b)
Reviewed-by: Qt Cherry-pick Bot <cherrypick_bot@qt-project.org>
---
 src/qml/CMakeLists.txt         | 27 ++++++++++++++++++++++++++-
 src/qml/Qt6QmlProperties.cmake | 26 ++++++++++++++++++++++++++
 2 files changed, 52 insertions(+), 1 deletion(-)
 create mode 100644 src/qml/Qt6QmlProperties.cmake

diff --git a/src/qml/CMakeLists.txt b/src/qml/CMakeLists.txt
index 407f4f5b9e..7de517f523 100644
--- a/src/qml/CMakeLists.txt
+++ b/src/qml/CMakeLists.txt
@@ -2,7 +2,7 @@
 # SPDX-License-Identifier: BSD-3-Clause
 
 include(Qt6QmlBuildInternals.cmake)
-
+include(Qt6QmlProperties.cmake)
 #####################################################################
 ## Qml Module:
 #####################################################################
@@ -437,10 +437,12 @@ qt_internal_add_qml_module(Qml
         "${CMAKE_CURRENT_LIST_DIR}/${INSTALL_CMAKE_NAMESPACE}QmltcFileMappingTemplate.qrc.in"
         "${CMAKE_CURRENT_LIST_DIR}/${INSTALL_CMAKE_NAMESPACE}QmlPublicCMakeHelpers.cmake"
         "${CMAKE_CURRENT_LIST_DIR}/${INSTALL_CMAKE_NAMESPACE}UpdateQmllsIni.cmake"
+        "${CMAKE_CURRENT_LIST_DIR}/${INSTALL_CMAKE_NAMESPACE}QmlProperties.cmake"
         ${extra_cmake_files}
     EXTRA_CMAKE_INCLUDES
         "${INSTALL_CMAKE_NAMESPACE}QmlFindQmlscInternal.cmake"
         "${INSTALL_CMAKE_NAMESPACE}QmlPublicCMakeHelpers.cmake"
+        "${INSTALL_CMAKE_NAMESPACE}QmlProperties.cmake"
         ${extra_cmake_includes}
     POLICIES
         QTP0001
@@ -622,6 +624,25 @@ qt_internal_extend_target(Qml CONDITION GCC AND QT_COMPILER_VERSION_MAJOR STREQU
         -fno-strict-aliasing
 )
 
+if(ANDROID)
+    string(JOIN "" application_type_condition
+        "$<STREQUAL:$<TARGET_PROPERTY:_qt_android_target_type>,APPLICATION>"
+    )
+else()
+    string(JOIN "" application_type_condition
+        "$<STREQUAL:$<TARGET_PROPERTY:TYPE>,EXECUTABLE>"
+    )
+endif()
+
+string(JOIN "" qt_qml_debug_definition
+    "$<$<AND:"
+            "${application_type_condition},"
+            "$<BOOL:$<TARGET_PROPERTY:QT_ENABLE_QML_DEBUG>>"
+        ">:"
+        "QT_QML_DEBUG"
+    ">"
+)
+
 qt_internal_extend_target(Qml CONDITION QT_FEATURE_qml_debug
     SOURCES
         debugger/qqmlabstractprofileradapter.cpp debugger/qqmlabstractprofileradapter_p.h
@@ -640,6 +661,10 @@ qt_internal_extend_target(Qml CONDITION QT_FEATURE_qml_debug
         jsruntime/qv4profiling.cpp
 )
 
+if(QT_FEATURE_qml_debug)
+    target_compile_definitions(Qml INTERFACE "${qt_qml_debug_definition}")
+endif()
+
 qt_internal_extend_target(Qml CONDITION UNIX
     SOURCES
         jsruntime/qv4compilationunitmapper_unix.cpp
diff --git a/src/qml/Qt6QmlProperties.cmake b/src/qml/Qt6QmlProperties.cmake
new file mode 100644
index 0000000000..4758d59cb4
--- /dev/null
+++ b/src/qml/Qt6QmlProperties.cmake
@@ -0,0 +1,26 @@
+# Copyright (C) 2025 The Qt Company Ltd.
+# SPDX-License-Identifier: BSD-3-Clause
+
+if(CMAKE_VERSION VERSION_GREATER_EQUAL "3.23")
+    set(_qt_qml_debug_extra_args
+        INITIALIZE_FROM_VARIABLE
+        QT_ENABLE_QML_DEBUG
+    )
+elseif(DEFINED QT_ENABLE_QML_DEBUG)
+    message(WARNING "QT_ENABLE_QML_DEBUG is set to ${QT_ENABLE_QML_DEBUG},"
+        " but the variable is not supported by this CMake version. Please set the"
+        " QT_ENABLE_QML_DEBUG target property where is required.")
+endif()
+
+define_property(TARGET
+    PROPERTY
+        QT_ENABLE_QML_DEBUG
+    BRIEF_DOCS
+        "Enables QML debugging for the specific target"
+    FULL_DOCS
+        "The property controls the QT_ENABLE_QML_DEBUG definition for the respective QML module.
+        The property is only applicable for Qt QML module applications."
+    ${_qt_qml_debug_extra_args}
+)
+
+unset(_qt_qml_debug_extra_args)
-- 
2.51.2

